$Pattern  Образец_Удаление_ненужных_поколений  : rule  {trace}
$Relevant_resources
  Общий            : Система     NoChange
  Особь            : Особи       Erase

$Body
  Общий
    Choice from Общий.Номер_поколения > Сколько_поколений_хранить
    first

  Особь
    Choice from Особь.Номер_поколения <= Общий.Номер_поколения - Сколько_поколений_хранить
    first
$End

$Pattern  Генерация_первой_популяции  : rule  {trace}
$Relevant_resources
  Общий      : Система     Keep
  Особь      : Особи       Create
$Body
  Общий
    Choice from Общий.Номер_поколения <= Количество_поколений and
                Общий.Режим = Инициализация and
                Общий.Счетчик < Общий.Размер_поколения
    first
    Convert_rule
      Счетчик          set Общий.Счетчик + 1

  Особь
    Convert_rule  trace
      Параметр_1           set Равномерно(0, 255)
      Параметр_2           set Равномерно(0, 255)
      Параметр_3           set Равномерно(0, 255)
      Параметр_4           set Равномерно(0, 255)
      Номер_поколения      set 1
      Номер_особи          set Общий.Счетчик
      Родитель_1           set 0
      Родитель_2           set 0
      Количество_потомков  set 0
      Скрещивание_байт     set 0
      Скрещивание_бит      set 0
      Мутация_байт         set 0
      Мутация_бит          set 0
      Статус               set не_рассмотрен
      Значение_ФП          set -1.0
      Сумма_до             set 0.0
      Сумма_после          set 0.0
$End

$Pattern  Конец_генерации_первого_поколения  : rule  {trace}
$Relevant_resources
  Общий      : Система     Keep
  Поколение  : Поколения   Create
$Body
  Общий
    Choice from Общий.Номер_поколения <= Количество_поколений and
                Общий.Режим = Инициализация and
                Общий.Счетчик = Общий.Размер_поколения
    first
    Convert_rule
      Номер_поколения  set 1
      Режим            set Расшифровка_и_расчет_ФП
      Счетчик          set 0
      Счетчик_бит      set 0

  Поколение
    Convert_rule  trace
      Номер                     set 1
      Размер                    set Общий.Размер_поколения
      Сумма_значений_ФП         set 0.0
      Среднее_значение_ФП       set 0.0
      Минимальное_значение_ФП   set 1.0e20
      Максимальное_значение_ФП  set -1.0e20
      Количество_скрещиваний    set 0
      Количество_мутаций        set 0
$End

$Pattern  Расшифровка_битов_и_расчет_ФП  : rule  {trace}
$Relevant_resources
  Общий            : Система             Keep
  Особь            : Особи               Keep
  Поколение        : Поколения           Keep
  Особь_для_показа : Особи_для_показа    Keep

$Body
  Общий
    Choice from Общий.Номер_поколения <= Количество_поколений and
                Общий.Режим = Расшифровка_и_расчет_ФП and
                Общий.Счетчик < Общий.Размер_поколения
    first
    Convert_rule
      Счетчик          set Общий.Счетчик + 1
      Счетчик_бит      set Общий.Счетчик_бит +
                           I_IfEQZero(Извлечь(Особь.Параметр_1, 3, 1), 0, 1)

  Особь
    Choice from Особь.Номер_поколения = Общий.Номер_поколения and
                Особь.Номер_особи = Общий.Счетчик + 1
    first
    Convert_rule
      Значение_ФП      set Вычисление_ФП((Извлечь(Особь.Параметр_1, 1, 8) +
                                         Извлечь(Особь.Параметр_2, 1, 8) / 256.0) / 256.0,
                                         (Извлечь(Особь.Параметр_3, 1, 8) +
                                         Извлечь(Особь.Параметр_4, 1, 8) / 256.0) / 256.0)
      Сумма_до         set Поколение.Сумма_значений_ФП
      Сумма_после      set Поколение.Сумма_значений_ФП + Особь.Значение_ФП

  Поколение
    Choice from Поколение.Номер = Общий.Номер_поколения
    first
    Convert_rule
      Сумма_значений_ФП         set Поколение.Сумма_значений_ФП + Особь.Значение_ФП
      Минимальное_значение_ФП   set RMin(Поколение.Минимальное_значение_ФП, Особь.Значение_ФП)
      Максимальное_значение_ФП  set RMax(Поколение.Максимальное_значение_ФП, Особь.Значение_ФП)

  Особь_для_показа
    Choice from Особь_для_показа.Номер_особи = Особь.Номер_особи
    first
    Convert_rule
		X_значение    set (Извлечь(Особь.Параметр_1, 1, 8) +
								Извлечь(Особь.Параметр_2, 1, 8) / 256.0) / 256.0
		Y_значение    set (Извлечь(Особь.Параметр_3, 1, 8) +
								Извлечь(Особь.Параметр_4, 1, 8) / 256.0) / 256.0
		X_координата  set {(Извлечь(Особь.Параметр_1, 1, 8) * 128 +
								Извлечь(Особь.Параметр_2, 1, 8) / 2) / 546}
								34 + Особь_для_показа.X_значение * 450
		Y_координата  set {(Извлечь(Особь.Параметр_3, 1, 8) * 128 +
								Извлечь(Особь.Параметр_4, 1, 8) / 2) / 1725}
								475 - Особь_для_показа.Y_значение * 450
$End

$Pattern  Образец_Конец_расшифровки  : rule  trace
$Relevant_resources
  Общий            : Система             Keep
  Поколение        : Поколения           Keep

$Body
  Общий
    Choice from Общий.Номер_поколения <= Количество_поколений and
                Общий.Режим = Расшифровка_и_расчет_ФП and
                Общий.Счетчик = Общий.Размер_поколения
    first
    Convert_rule
      Режим            set Воспроизведение
      Счетчик          set 0
      Число            set Розыгрыш_родителя(0.0, Поколение.Сумма_значений_ФП)

  Поколение
    Choice from Поколение.Номер = Общий.Номер_поколения
    first
    Convert_rule
      Среднее_значение_ФП       set Поколение.Сумма_значений_ФП / Общий.Размер_поколения
$End

$Pattern  Образец_Воспроизведение  : rule  {trace}
$Relevant_resources
  Общий            : Система             Keep
  Поколение        : Поколения           NoChange
  Родитель         : Особи               Keep
  Потомок          : Особи               Create

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Воспроизведение and
                Общий.Счетчик < Общий.Размер_поколения
    first
    Convert_rule
      Счетчик          set Общий.Счетчик + 1
      Число            set Розыгрыш_родителя(0.0, Поколение.Сумма_значений_ФП)

  Поколение
    Choice from Поколение.Номер = Общий.Номер_поколения
    first

  Родитель
    Choice from Родитель.Номер_поколения = Общий.Номер_поколения and
                Родитель.Сумма_до    <= Общий.Число and
                Родитель.Сумма_после >= Общий.Число
    first
    Convert_rule
      Количество_потомков  set Родитель.Количество_потомков + 1

  Потомок
    Convert_rule  {trace}
      Параметр_1           set Родитель.Параметр_1
      Параметр_2           set Родитель.Параметр_2
      Параметр_3           set Родитель.Параметр_3
      Параметр_4           set Родитель.Параметр_4
      Номер_поколения      set Общий.Номер_поколения + 1
      Номер_особи          set Общий.Счетчик
      Родитель_1           set Родитель.Номер_особи
      Родитель_2           set 0
      Количество_потомков  set 0
      Скрещивание_байт     set 0
      Скрещивание_бит      set 0
      Мутация_байт         set 0
      Мутация_бит          set 0
      Статус               set не_рассмотрен
      Значение_ФП          set -1.0
      Сумма_до             set 0.0
      Сумма_после          set 0.0
$End

$Pattern  Образец_Конец_воспроизведения  : rule  {trace}
$Relevant_resources
  Общий            : Система     Keep
  Поколение        : Поколения   Create

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Воспроизведение and
                Общий.Счетчик = Общий.Размер_поколения
    first
    Convert_rule
      Режим            set Скрещивание
      Счетчик          set 0
      Число            set 2.0

  Поколение
    Convert_rule  trace
      Номер                     set Общий.Номер_поколения + 1
      Размер                    set Общий.Размер_поколения
      Сумма_значений_ФП         set 0.0
      Среднее_значение_ФП       set 0.0
      Минимальное_значение_ФП   set 1.0e20
      Максимальное_значение_ФП  set -1.0e20
      Количество_скрещиваний    set 0
      Количество_мутаций        set 0
$End

$Pattern  Образец_Скрещивание  : rule  {trace}
$Relevant_resources
  Общий          : Система             Keep
  Поколение      : Поколения           Keep
  Гибрид_1       : Особи               Create
  Гибрид_2       : Особи               Create
  Особь_1        : Особи               Erase
  Особь_2        : Особи               Erase

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Скрещивание and
                Общий.Счетчик <= Общий.Размер_поколения / 2 and
                Общий.Число <= Общий.Вероятность_скрещивания
    first
    Convert_rule
      Число            set 2.0

  Поколение
    Choice from Поколение.Номер = Общий.Номер_поколения + 1
    first
    Convert_rule
      Количество_скрещиваний  set Поколение.Количество_скрещиваний + 1

  Гибрид_1
    Convert_rule  {trace}
      Параметр_1           set Параметр_гибрида(1, Особь_1.Параметр_1, Особь_2.Параметр_1)
      Параметр_2           set Параметр_гибрида(2, Особь_1.Параметр_2, Особь_2.Параметр_2)
      Параметр_3           set Параметр_гибрида(3, Особь_1.Параметр_3, Особь_2.Параметр_3)
      Параметр_4           set Параметр_гибрида(4, Особь_1.Параметр_4, Особь_2.Параметр_4)
      Номер_поколения      set Особь_1.Номер_поколения
      Номер_особи          set Особь_1.Номер_особи
      Родитель_1           set Особь_1.Родитель_1
      Родитель_2           set Особь_2.Родитель_1
      Количество_потомков  set Особь_1.Количество_потомков
      Скрещивание_байт     set Общий.Скрещивание_байт
      Скрещивание_бит      set Общий.Скрещивание_бит
      Мутация_байт         set Особь_1.Мутация_байт
      Мутация_бит          set Особь_1.Мутация_бит
      Статус               set Особь_1.Статус
      Значение_ФП          set Особь_1.Значение_ФП
      Сумма_до             set Особь_1.Сумма_до
      Сумма_после          set Особь_1.Сумма_после

  Гибрид_2
    Convert_rule  {trace}
      Параметр_1           set Параметр_гибрида(1, Особь_2.Параметр_1, Особь_1.Параметр_1)
      Параметр_2           set Параметр_гибрида(2, Особь_2.Параметр_2, Особь_1.Параметр_2)
      Параметр_3           set Параметр_гибрида(3, Особь_2.Параметр_3, Особь_1.Параметр_3)
      Параметр_4           set Параметр_гибрида(4, Особь_2.Параметр_4, Особь_1.Параметр_4)
      Номер_поколения      set Особь_2.Номер_поколения
      Номер_особи          set Особь_2.Номер_особи
      Родитель_1           set Особь_1.Родитель_1
      Родитель_2           set Особь_2.Родитель_1
      Количество_потомков  set Особь_2.Количество_потомков
      Скрещивание_байт     set Общий.Скрещивание_байт
      Скрещивание_бит      set Общий.Скрещивание_бит
      Мутация_байт         set Особь_2.Мутация_байт
      Мутация_бит          set Особь_2.Мутация_бит
      Статус               set Особь_2.Статус
      Значение_ФП          set Особь_2.Значение_ФП
      Сумма_до             set Особь_2.Сумма_до
      Сумма_после          set Особь_2.Сумма_после

  Особь_1
    Choice from Особь_1.Номер_поколения = Общий.Номер_поколения + 1 and
                Особь_1.Номер_особи = Общий.Родитель_1
    first

  Особь_2
    Choice from Особь_2.Номер_поколения = Общий.Номер_поколения + 1 and
                Особь_2.Номер_особи = Общий.Родитель_2
    first
$End

$Pattern  Образец_Подбор_пар  : rule  {trace}
$Relevant_resources
  Общий        : Система             Keep
  Особь_1      : Особи               Keep
  Особь_2      : Особи               Keep

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Скрещивание and
                Общий.Счетчик < Общий.Размер_поколения / 2
    first
    Convert_rule
      Счетчик          set Общий.Счетчик + 1
      Число            set Розыгрыш_скрещивания(0.0, 1.0)
      Родитель_1       set Особь_1.Номер_особи
      Родитель_2       set Особь_2.Номер_особи
      Скрещивание_байт set Выбор_байта(1, Количество_байтов_в_строке)
      Скрещивание_бит  set Выбор_бита(1, Предел_разрезания(Общий.Скрещивание_байт))

  Особь_1
    Choice from Особь_1.Номер_поколения = Общий.Номер_поколения + 1 and
                Особь_1.Статус = не_рассмотрен
    with_min Розыгрыш_скрещивания(0.0, 1.0)
    Convert_rule
      Статус           set рассмотрен

  Особь_2
    Choice from Особь_2.Номер_поколения = Общий.Номер_поколения + 1 and
                Особь_2.Статус = не_рассмотрен and
                Особь_2.Номер_особи <> Особь_1.Номер_особи and
                Особь_2.Родитель_1 <> Особь_1.Родитель_1
    with_min Розыгрыш_скрещивания(0.0, 1.0)
    Convert_rule
      Статус           set рассмотрен
$End

$Pattern  Образец_Конец_скрещивания  : rule  {trace}
$Relevant_resources
  Общий            : Система     Keep

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Скрещивание
    first
    Convert_rule
      Режим            set Мутация
      Счетчик          set 0
$End

$Pattern  Образец_мутация  : rule  {trace}
$Relevant_resources
  Общий            : Система             Keep
  Особь            : Особи               Keep
  Поколение        : Поколения           Keep

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Мутация and
                Общий.Счетчик < Общий.Размер_поколения
    first
    Convert_rule
      Счетчик          set Общий.Счетчик + 1
      Число            set Розыгрыш_мутации(0.0, 1.0)
      Мутация_байт     set Выбор_байта(1, Количество_байтов_в_строке)
      Мутация_бит      set Выбор_бита(1, Предел_мутации(Общий.Мутация_байт))

  Особь
    Choice from Особь.Номер_поколения = Общий.Номер_поколения + 1 and
                Особь.Номер_особи = Общий.Счетчик + 1
    first
    Convert_rule
      Параметр_1           set Если_мутация(Параметр_мутанта(1, Особь.Параметр_1), Особь.Параметр_1)
      Параметр_2           set Если_мутация(Параметр_мутанта(2, Особь.Параметр_2), Особь.Параметр_2)
      Параметр_3           set Если_мутация(Параметр_мутанта(3, Особь.Параметр_3), Особь.Параметр_3)
      Параметр_4           set Если_мутация(Параметр_мутанта(4, Особь.Параметр_4), Особь.Параметр_4)
      Мутация_байт         set Если_мутация(Общий.Мутация_байт, 0)
      Мутация_бит          set Если_мутация(Общий.Мутация_бит, 0)

  Поколение
    Choice from Поколение.Номер = Общий.Номер_поколения + 1
    first
    Convert_rule
      Количество_мутаций  set Поколение.Количество_мутаций + Если_мутация(1, 0)
$End

$Pattern  Образец_Конец_мутации  : rule  {trace}
$Relevant_resources
  Общий            : Система     Keep

$Body
  Общий
    Choice from Общий.Номер_поколения < Количество_поколений and
                Общий.Режим = Мутация and
                Общий.Счетчик = Общий.Размер_поколения
    first
    Convert_rule
      Номер_поколения  set Общий.Номер_поколения + 1
      Режим            set Расшифровка_и_расчет_ФП
      Счетчик          set 0
      Счетчик_бит      set 0
$End
