$Pattern  Образец_Приход_клиента : irregular_event  trace
$Relevant_resources
  Фигаро : Парикмахерская  Keep
$Time = {Равномерный_интервал(0.25, 0.75)}
		  Экспоненциальный_интервал(0.5)
		  {Нормальный_интервал(0.5, 0.2)}
		  {Заданный_интервал}
		  {Интервал_по_гистограмме}
$Body
  Фигаро
	 Convert_event
		Вход  set клиент_прибыл
$End

$Pattern  Образец_Размещение : rule  trace
$Relevant_resources
  Клиент : Клиенты         Keep
  Фигаро : Парикмахерская  Keep
$Body
  Клиент
	 Choice from  Клиент.Статус = нет
	 with_min  Случайное_число(0.0, 1.0)
	 Convert_rule
		Статус             set в_очереди
		Время_прихода      set Time_now
		Позиция_в_очереди  set Выбор_места_в_очереди(0)
  Фигаро
	 Choice from  Фигаро.Вход = клиент_прибыл and
					  Фигаро.Количество_клиентов_в_очереди < 6
	 first
	 Convert_rule
		Вход                           set нет_клиентов
		Количество_клиентов_в_очереди  set Фигаро.Количество_клиентов_в_очереди + 1
$End

$Pattern  Образец_Обслуживание_клиента : operation  trace
$Relevant_resources
  Фигаро : Парикмахерская  Keep  Keep
  Клиент : Клиенты         Keep  Keep
$Time = Нормальное_распределение_обслуживания(0.45, 0.2)
$Body
  Фигаро
	 Choice from Фигаро.Количество_клиентов_в_очереди > 0 and
					 Фигаро.Занятость_парикмахера = Свободен
		first
	 Convert_begin
		Занятость_парикмахера          set Занят
	 Convert_end
		Количество_клиентов_в_очереди  set Фигаро.Количество_клиентов_в_очереди - 1
		Занятость_парикмахера          set Свободен
		Обслужено_клиентов             set Фигаро.Обслужено_клиентов + 1
  Клиент
	 Choice from  Клиент.Статус = в_очереди
	 with_min Клиент.Время_прихода
	 Convert_begin
		Статус             set в_стрижке
		Позиция_в_очереди  set 0
	 Convert_end
		Статус             set нет
$End
