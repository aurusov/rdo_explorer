$Pattern Приход_Клиента_На_Почту : operation
$Relevant_resources
  Рел_Отделение : Отделения      NoChange NoChange
  Рел_Нагрузка  : Виды_Нагрузки  Keep     Keep
  Рел_Клиент    : Клиенты        Create   NoChange
$Time = Время_Прихода_Клиента(Рел_Нагрузка.Вид,
                              Рел_Нагрузка.Минимум_Среднее_Число,
                              Рел_Нагрузка.Максимум_Дисперсия)
$Body
  Рел_Отделение
    Choice from Рел_Отделение.Состояние = Рабочий_День
    first
  Рел_Нагрузка
    Choice from Рел_Нагрузка.Грузить = Да
    first
    Convert_begin
      Грузить set Нет
    Convert_end
      Грузить set Да
  Рел_Клиент
    Convert_begin
      Заявка           set Заявка_Клиента
      Состояние        set Возник
      Номер_В_Очереди  set 1
      Номер_Окна       set 1
      Приход_В_Очередь set Time_now
      Время_В_Очереди  set 0.0
$End

$Pattern Определить_Окно : rule
$Relevant_resources
  Рел_Клиент : Клиенты  Keep
  Рел_Окно   : Окна     NoChange
$Body
  Рел_Клиент
    Choice from Рел_Клиент.Состояние = Возник
    first
    Convert_rule
      Состояние  set Пришел
      Номер_Окна set Рел_Окно.Номер
  Рел_Окно
    Choice from [Рел_Окно.Номер = Заявки_Окна1(Рел_Клиент.Заявка) and Окно1.Работоспособность = Открыто] or
                [Рел_Окно.Номер = Заявки_Окна2(Рел_Клиент.Заявка) and Окно2.Работоспособность = Открыто] or
                [Рел_Окно.Номер = Заявки_Окна3(Рел_Клиент.Заявка) and Окно3.Работоспособность = Открыто] or
                [Рел_Окно.Номер = Заявки_Окна4(Рел_Клиент.Заявка) and Окно4.Работоспособность = Открыто] or
                [Рел_Окно.Номер = Заявки_Окна5(Рел_Клиент.Заявка) and Окно5.Работоспособность = Открыто]

    with_min(Рел_Окно.В_Очереди)
$End

$Pattern Ликвидирование : rule
$Relevant_resources
  Рел_Клиент            :  Клиенты            Erase
  Рел_Потерянный_Клиент : Потерянные_Клиенты  Keep
$Body
  Рел_Клиент
    Choice from Рел_Клиент.Состояние = Возник
    first
  Рел_Потерянный_Клиент
    Choice NoCheck
    first
    Convert_rule
      Количество set Рел_Потерянный_Клиент.Количество + 1
$End

$Pattern Открыть_Окно : rule
$Relevant_resources
  Рел_Время : Время  NoChange
  Рел_Окно  : Окна   Keep
$Body
  Рел_Время
    Choice NoCheck
    first
  Рел_Окно
    Choice from Рел_Окно.Работоспособность = Закрыто and
               [Рел_Окно.Интервал1_Начало = Рел_Время.Часы  or
                Рел_Окно.Интервал2_Начало = Рел_Время.Часы  or
                Рел_Окно.Интервал3_Начало = Рел_Время.Часы]
    first
    Convert_rule
      Работоспособность set Открыто
$End

$Pattern Закрыть_Окно : rule
$Relevant_resources
  Рел_Время : Время  NoChange
  Рел_Окно  : Окна   Keep
$Body
  Рел_Время
    Choice NoCheck
    first
  Рел_Окно
    Choice from Рел_Окно.Работоспособность = Открыто and
               [Рел_Окно.Интервал1_Конец = Рел_Время.Часы  or
                Рел_Окно.Интервал2_Конец = Рел_Время.Часы  or
                Рел_Окно.Интервал3_Конец = Рел_Время.Часы]
    first
    Convert_rule
      Работоспособность set Закрыто
$End

$Pattern Приход_Клиента_В_Очередь : rule
$Relevant_resources
  Рел_Клиент : Клиенты  Keep
  Рел_Окно   : Окна     Keep
$Body
  Рел_Клиент
    Choice from Рел_Клиент.Состояние = Пришел
    first
    Convert_rule
      Состояние        set В_Очереди
      Номер_В_Очереди  set Рел_Окно.В_Очереди + 1
      Приход_В_Очередь set Time_now
  Рел_Окно
    Choice from Рел_Окно.Номер = Рел_Клиент.Номер_Окна
    first
    Convert_rule
      В_Очереди set Рел_Окно.В_Очереди + 1
$End

$Pattern Сдвинуть_Очередь : rule
$Parameters
  Номер : such_as Окна.Номер
$Relevant_resources
  Рел_Счетчик : Счетчики  Keep
  Рел_Клиент  : Клиенты   Keep
$Body
  Рел_Счетчик
    Choice from Рел_Счетчик.Номер = Номер
    first
    Convert_rule
      Текущий_Клиент set Рел_Счетчик.Текущий_Клиент + 1
  Рел_Клиент
    Choice from Рел_Клиент.Номер_В_Очереди = Рел_Счетчик.Текущий_Клиент  and
                Рел_Клиент.Номер_Окна = Номер
    first
    Convert_rule
      Номер_В_Очереди set Рел_Клиент.Номер_В_Очереди - 1
$End

$Pattern Функционирование : operation
$Relevant_resources
  Рел_Отделение : Отделения  Keep Keep
$Time = Время_Рабочего_Дня
$Body
  Рел_Отделение
    Choice from Рел_Отделение.Состояние = Начало_Дня
    first
    Convert_begin
      Состояние set Рабочий_День
    Convert_end
      Состояние set Конец_Дня
$End

$Pattern Обслуживание_Первого : operation
$Parameters
  Номер : such_as Окна.Номер
$Relevant_resources
  Рел_Отделение       : Отделения             NoChange NoChange
  Рел_Окно            : Окна                  Keep     Keep
  Рел_Клиент          : Клиенты               Keep     Keep
  Рел_Счетчик         : Счетчики              Keep     NoChange
  Рел_Счетчик_Заявок  : Счетчики_Заявок       NoChange Keep
  Рел_Счетчик_Времени : Счетчики_Времени      NoChange Keep
  Рел_Из_Клиент       : Изображаемые_Клиенты  Keep     NoChange
$Time = Коэф_Времени * Время_Обслуживания(Рел_Клиент.Заявка)
$Body
  Рел_Отделение
    Choice from Рел_Отделение.Состояние = Рабочий_День
    first
  Рел_Окно
    Choice from Рел_Окно.Номер = Номер and
                Рел_Окно.Занятость = Свободен
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
      Обслужено set Рел_Окно.Обслужено + 1
      В_Очереди set Рел_Окно.В_Очереди - 1
  Рел_Клиент
    Choice from  Рел_Клиент.Состояние = В_Очереди and
                 Рел_Клиент.Номер_Окна = Номер and
                 Рел_Окно.Занятость = Свободен and
                 Рел_Клиент.Номер_В_Очереди = 1
    first
    Convert_begin
      Состояние       set Обслуживается
      Время_В_Очереди set Time_now - Рел_Клиент.Приход_В_Очередь
    Convert_end
      Состояние      set  Ушел
  Рел_Счетчик
    Choice from Рел_Счетчик.Номер = Номер and
                Рел_Отделение.Состояние = Рабочий_День
    first
    Convert_begin
      Текущий_Клиент set 2
  Рел_Счетчик_Заявок
    Choice from Рел_Счетчик_Заявок.Заявка = Рел_Клиент.Заявка
    first
    Convert_end
      Количество set Рел_Счетчик_Заявок.Количество + 1
  Рел_Счетчик_Времени
    Choice from Рел_Счетчик_Времени.Номер = Номер
    first
    Convert_end
      Время_На_Обработку set Рел_Счетчик_Времени.Время_На_Обработку +
                               ВремечкО(Рел_Клиент.Заявка)
 Рел_Из_Клиент
   Choice from Рел_Из_Клиент.Номер = Номер
   first
   Convert_begin
     Заявка set Рел_Клиент.Заявка
$End

$Pattern Уничтожение : rule
$Parameters
  Номер : such_as Окна.Номер
$Relevant_resources
  Рел_Отделение : Отделения  NoChange
  Рел_Окно      : Окна       Keep
  Рел_Клиент    : Клиенты    Erase
$Body
  Рел_Отделение
    Choice from Рел_Отделение.Состояние = Конец_Дня
    first
  Рел_Окно
    Choice from Рел_Окно.Номер = Номер and
                Рел_Окно.Занятость = Свободен and
                Рел_Окно.В_Очереди > 0
    first
    Convert_rule
      В_Очереди set Рел_Окно.В_Очереди - 1
  Рел_Клиент
    Choice from Рел_Клиент.Состояние = В_Очереди and
                Рел_Клиент.Номер_Окна = Номер
    first
$End

$Pattern Переход_К_Новому_Дню : rule
$Relevant_resources
  Рел_Отделение        : Отделения         Keep
  Рел_Счетчик_Дней     : Счетчики_Дней     Keep
  Рел_Счетчик_Времени1 : Счетчики_Времени  Keep
  Рел_Счетчик_Времени2 : Счетчики_Времени  Keep
  Рел_Счетчик_Времени3 : Счетчики_Времени  Keep
  Рел_Счетчик_Времени4 : Счетчики_Времени  Keep
  Рел_Счетчик_Времени5 : Счетчики_Времени  Keep
  Рел_Время            : Время             Keep
$Body
  Рел_Отделение
    Choice from Окно1.В_Очереди + Окно2.В_Очереди + Окно3.В_Очереди +
                Окно4.В_Очереди + Окно5.В_Очереди = 0 and
                Рел_Отделение.Состояние = Конец_Дня
    first
    Convert_rule
      Состояние set Начало_Дня
  Рел_Счетчик_Дней
    Choice  NoCheck
    first
    Convert_rule
      Количество_Дней set Рел_Счетчик_Дней.Количество_Дней + 1
  Рел_Счетчик_Времени1
    Choice from Рел_Счетчик_Времени1.Номер = 1
    first
    Convert_rule
      Время_На_Обработку set 0.0
  Рел_Счетчик_Времени2
    Choice from Рел_Счетчик_Времени2.Номер = 2
    first
    Convert_rule
      Время_На_Обработку set 0.0
  Рел_Счетчик_Времени3
    Choice from Рел_Счетчик_Времени3.Номер = 3
    first
    Convert_rule
      Время_На_Обработку set 0.0
  Рел_Счетчик_Времени4
    Choice from Рел_Счетчик_Времени4.Номер = 4
    first
    Convert_rule
     Время_На_Обработку set 0.0
  Рел_Счетчик_Времени5
    Choice from Рел_Счетчик_Времени5.Номер = 5
    first
    Convert_rule
      Время_На_Обработку set 0.0
  Рел_Время
    Choice NoCheck
    first
    Convert_rule
      Часы                set 9.0
      Последнее_Изменение set Time_now
$End

$Pattern Уход_Клиента : rule
$Parameters
  Номер : such_as Окна.Номер
$Relevant_resources
  Рел_Окно    : Окна     NoChange
  Рел_Клиент  : Клиенты  Erase
$Body
  Рел_Окно
    Choice from Рел_Окно.Номер = Номер
    first
  Рел_Клиент
    Choice from Рел_Клиент.Состояние = Ушел
    first
$End

$Pattern Обслуживание : keyboard
$Parameters
  Номер : such_as  Окна.Номер
$Relevant_resources
  Рел_Смотритель : Смотрители  Keep NoChange
$Time = 0.0
$Body
  Рел_Смотритель
    Choice from Рел_Смотритель.Номер  =  Номер
    first
    Convert_begin
      Разрешить set Показать_Спрятать(Рел_Смотритель.Разрешить,Номер)
$End

$Pattern Соответствие_Времени : operation
$Relevant_resources
  Рел_Время : Время  Keep Keep
$Time = 1.0
$Body
  Рел_Время
    Choice from Рел_Время.Соответствие = Нет and
                (Time_now - Рел_Время.Последнее_Изменение) >= 1.0
    first
    Convert_begin
      Часы                set Рел_Время.Часы + 1
      Соответствие        set Да
      Последнее_Изменение set Time_now
    Convert_end
      Соответствие        set Нет
$End

