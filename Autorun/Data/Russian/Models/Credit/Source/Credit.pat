{1}
$Pattern Появление_клиента_в_системе_ie : irregular_event trace
$Relevant_resources
  Номер : Номер_r  Keep
  Товар : Товар_tt Create
  Кредитная__карточка : Кредитная__карточка_tt Create
$Time=Появление_клиента_в_системе_s(5,10)
$Body
  Номер
    Convert_event
      Номер set Номер.Номер + 1
      В_системе set Номер.В_системе + 1
  Товар
    Convert_event
      Сумма set Товар_Сумма_s(20)
      Наименование set Товар_Наименование_s
      Номер set Номер.Номер
      Товар_f_A1 set 0
      Товар_f_A44 set 0
  Кредитная__карточка
    Convert_event
      Сумма set Кредит_карт_Сумма_s(30)
      Номер set Номер.Номер
      Номер_очереди set Номер_очереди_f(Номер.Длина_очереди_1, Номер.Длина_очереди_2)
      Кредитная__карточка_f_A2 set 0
      Кредитная__карточка_f_A43 set 0
      Кредитная__карточка_f_A42 set 0
$End

{2}
$Pattern Оценивание_покупки_и_постановка_в_очередь_pat : operation trace
$Relevant_resources
  Кредитная__карточка : Кредитная__карточка_tt Keep NoChange
  Номер2 : Номер_pt Keep NoChange
  Товар  : Товар_tt Keep     NoChange
  Цена   : Цена_tt  NonExist Create
$Time=20
$Body
  Кредитная__карточка
    Choice from [Кредитная__карточка.Номер_очереди = перед_первой] or
                [Кредитная__карточка.Номер_очереди = перед_второй]
    first
    Convert_begin
      Номер_очереди set Номер_очереди_f1(Кредитная__карточка.Номер_очереди)
  Номер2
    Choice NoCheck
    first
    Convert_begin
      Длина_очереди_1 set Номер2.Длина_очереди_1+Длина_очереди_f(Кредитная__карточка.Номер_очереди, первая)
      Длина_очереди_2 set Номер2.Длина_очереди_2+Длина_очереди_f(Кредитная__карточка.Номер_очереди, вторая)
  Товар
    Choice from [Товар.Товар_f_A1 = 0]
    first
    Convert_begin
      Товар_f_A1 set 1
  Цена
    Convert_end
      Сумма set Товар.Сумма
      Номер set Товар.Номер
      Цена_f_A3  set 0
      Цена_f_A41 set 0
      Цена_f_A42 set 0
      Цена_f_A43 set 0
$End

{3}
$Pattern Идентификация_клиента_pat : operation trace
$Relevant_resources
  Кредитная__карточка  : Кредитная__карточка_tt  Keep NoChange
  Автомат_для_карточки : Автомат_для_карточки_pt Keep Keep
  Счет  : Счет_tt  NonExist Create
  Сеть1 : Сеть_lan Keep     Keep
  Сеть2 : Сеть_wan Keep     Keep
$Time=5
$Body
  Кредитная__карточка
    Choice from [Кредитная__карточка.Кредитная__карточка_f_A2 = 0]
    first
    Convert_begin
      Кредитная__карточка_f_A2 set 1
  Автомат_для_карточки
    Choice from [Автомат_для_карточки.Занятость = Свободен] and
                [Автомат_для_карточки.Номер_очереди = Кредитная__карточка.Номер_очереди]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Счет
    Convert_end
      Сумма set Кредитная__карточка.Сумма
      Номер set Кредитная__карточка.Номер
      Счет_f_A3 set 0
  Сеть1
    Choice from [Сеть1.Занятость > 0]
    first
    Convert_begin
      Занятость set Сеть1.Занятость - 1
    Convert_end
      Занятость set Сеть1.Занятость + 1
  Сеть2
    Choice from [Сеть2.Занятость > 0]
    first
    Convert_begin
      Занятость set Сеть2.Занятость - 1
    Convert_end
      Занятость set Сеть2.Занятость + 1
$End

{4}
$Pattern Проверка_платежеспособности_pat : operation trace
$Relevant_resources
  Цена : Цена_tt  Keep NoChange
  Счет : Счет_tt  Keep NoChange
  PC   : PC_pt    Keep Keep
  Сеть : Сеть_lan Keep Keep
  Разрешение_оплаты : Разрешение_оплаты_tt NonExist Create
$Time=1
$Body
  Цена
    Choice from [Цена.Цена_f_A3 = 0]
    first
    Convert_begin
      Цена_f_A3 set 1
  Счет
    Choice from [Счет.Счет_f_A3 = 0] and
                [Счет.Номер = Цена.Номер]
    first
    Convert_begin
      Счет_f_A3 set 1
  PC
    Choice from [PC.Занятость = Свободен]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Сеть
    Choice from [Сеть.Занятость > 0]
    first
    Convert_begin
      Занятость set Сеть.Занятость - 1
    Convert_end
      Занятость set Сеть.Занятость + 1
  Разрешение_оплаты
    Convert_end
      Разрешение_оплаты_m set Проверка_платеж_f(Счет.Сумма, Цена.Сумма)
      Номер set Цена.Номер
      Разрешение_оплаты_f_A41 set 0
      Разрешение_оплаты_f_A42 set 0
      Разрешение_оплаты_f_A44 set 0
      Разрешение_оплаты_f_A43 set 0
$End

{5}
$Pattern Копии_счетов_pat : operation trace
$Relevant_resources
  Кредитная__карточка : Кредитная__карточка_tt NoChange NoChange
  Цена : Цена_tt Keep NoChange
  Кассовый__аппарат : Кассовый__аппарат_pt Keep Keep
  Разрешение_оплаты : Разрешение_оплаты_tt Keep NoChange
  Чек : Чек_tt NonExist Create
$Time=5
$Body
  Кредитная__карточка
    Choice NoCheck
    first
  Цена
    Choice from [Цена.Цена_f_A41 = 0]
    first
    Convert_begin
      Цена_f_A41 set 1
  Кассовый__аппарат
    Choice from [Кассовый__аппарат.Занятость = Свободен] and
                [Кассовый__аппарат.Номер_очереди = Кредитная__карточка.Номер_очереди]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_m = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A41 = 0] and
                [Разрешение_оплаты.Номер = Цена.Номер]
    first
    Convert_begin
      Разрешение_оплаты_f_A41 set 1
  Чек
    Convert_end
      Сумма set Цена.Сумма
      Номер set Цена.Номер
$End

{6}
$Pattern Обновление_файла_клиента_pat : operation trace
$Relevant_resources
  Кредитная__карточка : Кредитная__карточка_tt Keep NoChange
  Цена : Цена_tt  Keep NoChange
  PC   : PC_pt    Keep Keep
  Сеть : Сеть_wan Keep Keep
  Разрешение_оплаты      : Разрешение_оплаты_tt      Keep     NoChange
  Новые_данные_о_клиенте : Новые_данные_о_клиенте_tt NonExist Create
$Time=3
$Body
  Кредитная__карточка
    Choice from [Кредитная__карточка.Кредитная__карточка_f_A42 = 0] and
                [[Кредитная__карточка.Номер_очереди = первая] or
                 [Кредитная__карточка.Номер_очереди = вторая]]
    first
    Convert_begin
      Кредитная__карточка_f_A42 set 1
  Цена
    Choice from [Цена.Цена_f_A42 = 0] and
                [Цена.Номер = Кредитная__карточка.Номер]
    first
    Convert_begin
      Цена_f_A42 set 1
  PC
    Choice from [PC.Занятость = Свободен]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Сеть
    Choice from [Сеть.Занятость > 0]
    first
    Convert_begin
      Занятость set Сеть.Занятость - 1
    Convert_end
      Занятость set Сеть.Занятость + 1
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_m = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A42 = 0] and
                [Разрешение_оплаты.Номер = Кредитная__карточка.Номер]
    first
    Convert_begin
      Разрешение_оплаты_f_A42 set 1
  Новые_данные_о_клиенте
    Convert_end
      Сумма set Кредитная__карточка.Сумма
      Номер set Кредитная__карточка.Номер
$End

{7}
$Pattern Запись_на_кредитную_карточку_pat : operation trace
$Relevant_resources
  Кредитная__карточка  : Кредитная__карточка_tt  Keep     NoChange
  Цена                 : Цена_tt                 Keep     NoChange
  Автомат_для_карточки : Автомат_для_карточки_pt Keep     Keep
  Разрешение_оплаты    : Разрешение_оплаты_tt    Keep     NoChange
  Кредитная__карточка1 : Кредитная__карточка1_tt NonExist Create
$Time=2
$Body
  Кредитная__карточка
    Choice from [Кредитная__карточка.Кредитная__карточка_f_A43 = 0]
    first
    Convert_begin
      Кредитная__карточка_f_A43 set 1
  Цена
    Choice from [Цена.Цена_f_A43 = 0] and
                [Цена.Номер = Кредитная__карточка.Номер]
    first
    Convert_begin
      Цена_f_A43 set 1
  Автомат_для_карточки
    Choice from [Автомат_для_карточки.Занятость = Свободен] and
                [Автомат_для_карточки.Номер_очереди = Кредитная__карточка.Номер_очереди]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_m = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A43 = 0] and
                [Разрешение_оплаты.Номер = Кредитная__карточка.Номер]
    first
    Convert_begin
      Разрешение_оплаты_f_A43 set 1
  Кредитная__карточка1
    Convert_end
      Сумма set Кредитная__карточка.Сумма
      Номер set Кредитная__карточка.Номер
$End

{8}
$Pattern Формирование_заказа_pat : operation trace
$Relevant_resources
  Товар : Товар_tt Keep     NoChange
  PC    : PC_pt    Keep     Keep
  Сеть  : Сеть_wan Keep     Keep
  Заказ : Заказ_tt NonExist Create
  Разрешение_оплаты : Разрешение_оплаты_tt Keep NoChange
$Time=3
$Body
  Товар
    Choice from [Товар.Товар_f_A44 = 0]
    first
    Convert_begin
      Товар_f_A44 set 1
  PC
    Choice from [PC.Занятость = Свободен]
    first
    Convert_begin
      Занятость set Занят
    Convert_end
      Занятость set Свободен
  Сеть
    Choice from [Сеть.Занятость > 0]
    first
    Convert_begin
      Занятость set Сеть.Занятость - 1
    Convert_end
      Занятость set Сеть.Занятость + 1
  Заказ
    Convert_end
      Сумма set Товар.Сумма
      Номер set Товар.Номер
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_m = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A44 = 0] and
                [Разрешение_оплаты.Номер = Товар.Номер]
    first
    Convert_begin
      Разрешение_оплаты_f_A44 set 1
$End

{9}
$Pattern Завершение_работы_rul : rule trace
$Relevant_resources
  Номер2 : Номер_pt Keep
  Товар  : Товар_tt Erase
  Кредитная__карточка  : Кредитная__карточка_tt Erase
  Кредитная__карточка1 : Кредитная__карточка1_tt Erase
  Заказ : Заказ_tt Erase
  Чек   : Чек_tt Erase
  Разрешение_оплаты : Разрешение_оплаты_tt Erase
  Цена  : Цена_tt Erase
  Счет  : Счет_tt Erase
  Новые_данные_о_клиенте : Новые_данные_о_клиенте_tt Erase
$Body
  Номер2
    Choice NoCheck
    first
    Convert_rule
      Вышло     set Номер2.Вышло + 1
      В_системе set Номер2.В_системе - 1
      Длина_очереди_1 set Номер2.Длина_очереди_1-Длина_очереди_f(Кредитная__карточка.Номер_очереди, первая)
      Длина_очереди_2 set Номер2.Длина_очереди_2-Длина_очереди_f(Кредитная__карточка.Номер_очереди, вторая)
  Товар
    Choice from [Товар.Товар_f_A1 = 1] and
                [Товар.Товар_f_A44 = 1]
    first
  Кредитная__карточка
    Choice from [Кредитная__карточка.Кредитная__карточка_f_A2 = 1] and
                [Кредитная__карточка.Кредитная__карточка_f_A43 = 1] and
                [Кредитная__карточка.Кредитная__карточка_f_A42 = 1] and
                [Кредитная__карточка.Номер = Товар.Номер] and
                [[Кредитная__карточка.Номер_очереди = первая] or
                 [Кредитная__карточка.Номер_очереди = вторая]]
    first
  Кредитная__карточка1
    Choice from [Кредитная__карточка1.Номер = Товар.Номер]
    first
  Заказ
    Choice from [Заказ.Номер = Товар.Номер]
    first
  Чек
    Choice from [Чек.Номер = Товар.Номер]
    first
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_f_A41 = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A42 = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A44 = 1] and
                [Разрешение_оплаты.Разрешение_оплаты_f_A43 = 1] and
                [Разрешение_оплаты.Номер = Товар.Номер]
    first
  Цена
    Choice from [Цена.Цена_f_A3 = 1] and
                [Цена.Цена_f_A41 = 1] and
                [Цена.Цена_f_A42 = 1] and
                [Цена.Цена_f_A43 = 1] and
                [Цена.Номер = Товар.Номер]
    first
  Счет
    Choice from [Счет.Счет_f_A3 = 1] and
                [Счет.Номер = Товар.Номер]
    first
  Новые_данные_о_клиенте
    Choice from [Новые_данные_о_клиенте.Номер = Товар.Номер]
    first
$End

{10}
$Pattern Нет_разрешения_rul : rule trace
$Relevant_resources
  Номер2 : Номер_pt Keep
  Разрешение_оплаты : Разрешение_оплаты_tt Erase
  Товар : Товар_tt Erase
  Кредитная__карточка : Кредитная__карточка_tt Erase
  Цена : Цена_tt Erase
  Счет : Счет_tt Erase
$Body
  Номер2
    Choice NoCheck
    first
    Convert_rule
      Вышло     set Номер2.Вышло + 1
      В_системе set Номер2.В_системе - 1
      Длина_очереди_1 set Номер2.Длина_очереди_1-Длина_очереди_f(Кредитная__карточка.Номер_очереди, первая)
      Длина_очереди_2 set Номер2.Длина_очереди_2-Длина_очереди_f(Кредитная__карточка.Номер_очереди, вторая)
  Разрешение_оплаты
    Choice from [Разрешение_оплаты.Разрешение_оплаты_m = 0]
    first
  Товар
    Choice from [Товар.Номер = Разрешение_оплаты.Номер]
    first
  Кредитная__карточка
    Choice from [Кредитная__карточка.Номер = Разрешение_оплаты.Номер] and
                [[Кредитная__карточка.Номер_очереди = первая] or
                 [Кредитная__карточка.Номер_очереди = вторая]]
    first
  Цена
    Choice from [Цена.Номер = Разрешение_оплаты.Номер]
    first
  Счет
    Choice from [Счет.Номер = Разрешение_оплаты.Номер]
    first
$End
