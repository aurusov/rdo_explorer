$Pattern Генерация_запуска : rule
$Relevant_resources
  Система_р : Система  Keep
$Body
  Система_р
    Choice from Система_р.Номер_детали = 0 and Система_р.Состояние = Приготовление
      first
      Convert_rule
        Состояние      set Оп_Генерация
        Номер_детали   set 0
        Обраб_деталей  set 0
        Всего_детал    set 0
$End

$Pattern Приход_партии_тип : irregular_event
$Parameters
  _тип    : such_as Система.Тип
  Число_1 : real
  Число_2 : real
$Relevant_resources
  Сист : Систем  Keep
$Time = Генератор_заказов(_тип, Число_1, Число_2)
$Body
   Сист
     Convert_event
       Состояние set Оп_Генерация
       Тип       set _тип
$End

$Pattern Поломка_ОТО : irregular_event
$Parameters
  _Номер  : such_as ОТО.Пор_ном_ОТО
  Число_1 : real
  Число_2 : real
$Relevant_resources
  Сист : Систем  Keep
$Time = Генератор_аварий(_Номер, Число_1, Число_2)
$Body
  Сист
    Convert_event
      Номер_ОТО set _Номер
      Авария    set Да
$End

$Pattern Инициализация_аварии : rule
$Relevant_resources
  Система_2 : Система  Keep
  Ото_р     : ОТО      Keep
$Body
  Система_2
    Choice from Система_2.Авария = Да
      first
      Convert_rule
        Авария set Нет
  Ото_р
    Choice from Ото_р.Пор_ном_ОТО = Система_2.Номер_ОТО and
                Ото_р.Состояние = Занято and
                Ото_р.Обраб_дет > 0
      first
      Convert_rule
        Авария set Да
$End

$Pattern Начало_Генерации : rule
$Relevant_resources
  Система_2 : Система  Keep
  Тип_р     : Типы     Keep
$Body
  Система_2
    Choice from Система_2.Состояние = Оп_Генерация
      first
      Convert_rule
        Состояние   set Генерация
        Всего_детал set Система_2.Всего_детал + Загрузить(Тип_р.Всего_детал, Тип_р.Объем_выпуска, Тип_р.Партия_зап)
  Тип_р
    Choice from Тип_р.Тип = Система_2.Тип
      first
      Convert_rule
        Всего_детал set Тип_р.Всего_детал + Загрузить(Тип_р.Всего_детал, Тип_р.Объем_выпуска, Тип_р.Партия_зап)
$End

$Pattern Генерация_деталей : rule
$Relevant_resources
  Система_2  : Система     Keep
  Пер_партия : Пер_партии  Create
  Тип_р      : Типы        Keep
$Body
  Система_2
    Choice from  Система_2.Состояние = Генерация
      first
      Convert_rule
        Номер_детали   set Система_2.Номер_детали + 1
   Пер_партия
      Convert_rule
        Тип            set Система_2.Тип
        Размер         set Загрузить(Тип_р.Сгенерировано, Тип_р.Всего_детал, Тип_р.Перед_партия)
        Номер          set Система_2.Номер_детали
        Номер_операции set 1
        Сост           set Ожидание
        Транспорт      set 0
        Место          set Складвх
        Сл_место       set Складвх
   Тип_р
     Choice from Тип_р.Тип = Система_2.Тип and Тип_р.Сгенерировано < Тип_р.Всего_детал
      first
      Convert_rule
        Сгенерировано  set Тип_р.Сгенерировано + Загрузить(Тип_р.Сгенерировано, Тип_р.Всего_детал, Тип_р.Перед_партия)
$End

$Pattern Конец_Генерации : rule
$Relevant_resources
  Сист  : Система  Keep
  Тип_р : Типы     NoChange
$Body
  Сист
    Choice from Сист.Состояние = Генерация and Сист.Всего_детал <> 0
      first
      Convert_rule
        Состояние set Оп_Запуск
  Тип_р
    Choice from Тип_р.Тип = Сист.Тип and Тип_р.Сгенерировано = Тип_р.Всего_детал
      first
$End

$Pattern Запуск_партии : rule
$Relevant_resources
  Система_2  : Система     Keep
  Пер_партия : Пер_партии  Keep
  ТУ_р       : ТУ          Keep
  Накоп      : Накопители  Keep
$Body
  Система_2
    Choice from Система_2.Состояние = Оп_Запуск
      first
      Convert_rule
        Номер_порядк set Система_2.Номер_порядк + 1
  Пер_партия
    Choice from  Пер_партия.Сост = Ожидание and Пер_партия.Место = Складвх
      first
      Convert_rule
        Сост         set Перевозка
        Транспорт    set ТУ_р.Номер
        Сл_место     set Тех_путь(Пер_партия.Номер_операции,Пер_партия.Тип)
  ТУ_р
    Choice from ТУ_р.Состояние = Свободен and
		Нак_ТУ(Пер_партия.Место, ТУ_р.Номер) = 1  and
		Нак_ТУ(Пер_партия.Сл_место, ТУ_р.Номер) = 1
      with_min Нак_Узлы(Пер_партия.Место) - Нак_Узлы(ТУ_р.Позиция)
      Convert_rule
        Состояние    set Вызов
        Куда         set Складвх
   Накоп
    Choice from Накоп.Идентификатор = Тех_путь(Пер_партия.Номер_операции,Пер_партия.Тип) and
                Накоп.Свободно >= Пер_партия.Размер
      first
      Convert_rule
        Свободно     set Накоп.Свободно - Пер_партия.Размер
$End

$Pattern Начало_движения : rule
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Вызов
      first
      Convert_rule
        Состояние   set Проверка_узла
        Сл_величина set 0.0
        Номер_шага  set 0
        Номер_узла  set Нак_Узлы(Роб_1.Позиция)
        Приоритет   set 0.0
$End

$Pattern Начало_проверки : rule
$Parameters
  Вход  : such_as ТУ.Состояние
  Выход : such_as ТУ.Состояние
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Вход and
                Роб_1.Номер_узла <> Нак_Узлы(Роб_1.Куда) and
                Роб_1.Номер_шага < Количество_узлов
      first
      Convert_rule
        Состояние  set Выход
        Номер_шага set Роб_1.Номер_шага + 1
$End

$Pattern Пропуск_связи : rule
$Parameters
  Вход  : such_as ТУ.Состояние
  Выход : such_as ТУ.Состояние
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Вход and
                Расстояние(Роб_1.Номер_узла, Роб_1.Номер_шага) = 0
      first
      Convert_rule
        Состояние set Выход
$End

$Pattern Проверка_цели : rule
$Parameters
  Вход  : such_as ТУ.Состояние
  Выход : such_as ТУ.Состояние
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Вход
      first
      Convert_rule
        Состояние   set Выход
        Сл_величина set Возврат(Роб_1.Пред_узел, Нак_Узлы(Роб_1.Куда), Роб_1.Номер_шага)
        След_узел   set _Сравнить_узлы(Роб_1.Приоритет, Роб_1.Сл_величина) * Роб_1.Номер_шага + _Сравнить_узлы(Роб_1.Сл_величина, Роб_1.Приоритет) * Роб_1.След_узел
	Приоритет   set  Max(Роб_1.Приоритет, Роб_1.Сл_величина)
$End

$Pattern Конец_проверки : rule
$Parameters
  Вход  : such_as ТУ.Состояние
  Выход : such_as ТУ.Состояние
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Вход and
                Роб_1.Номер_узла <> Нак_Узлы(Роб_1.Куда) and
                Роб_1.Номер_шага = Количество_узлов
      first
      Convert_rule
        Состояние set Выход
$End

$Pattern Передвижение : operation
$Relevant_resources
  Роб_1 : ТУ  Keep Keep
$Time = (Расстояние(Роб_1.Номер_узла, Роб_1.След_узел) / Роб_1.Скорость) * Масштаб_времени
$Body
  Роб_1
    Choice from Роб_1.Состояние = Проверено and Роб_1.Номер_узла <> Нак_Узлы(Роб_1.Куда) and
                Расстояние(Роб_1.Номер_узла, Роб_1.След_узел) > 0
     first
     Convert_begin
       Состояние  set Движение_св
       Пред_узел  set Роб_1.Номер_узла
       Х_коор     set Координаты_узлов(Х,Роб_1.Номер_узла)
       У_коор     set Координаты_узлов(У,Роб_1.Номер_узла)
     Convert_end
       Состояние  set Проверка_узла
       Сл_величина set 0.0
       Номер_шага set 0
       Номер_узла set Роб_1.След_узел
       Х_коор     set Координаты_узлов(Х,Роб_1.Номер_узла)
       У_коор     set Координаты_узлов(У,Роб_1.Номер_узла)
       Приоритет  set 0.0
$End

$Pattern Конец_движения : rule
$Relevant_resources
  Роб_1      : ТУ          Keep
  Пер_партия : Пер_партии  NoChange
$Body
  Роб_1
    Choice from Роб_1.Состояние = Проверка_узла and Роб_1.Номер_узла = Нак_Узлы(Роб_1.Куда)
      first
      Convert_rule
        Позиция   set Роб_1.Куда
        Состояние set Загрузка
        Куда      set Пер_партия.Сл_место
  Пер_партия
    Choice from Пер_партия.Сост = Перевозка and Пер_партия.Транспорт = Роб_1.Номер
      first
$End

$Pattern Загрузка_ТУ : operation
$Relevant_resources
  ТУ_р       : ТУ          Keep     Keep
  Пер_партия : Пер_партии  Keep     NoChange
  Накоп      : Накопители  NoChange Keep
$Time = ТУ_р.Скорость_пог * Пер_партия.Размер * Масштаб_времени
$Body
  ТУ_р
    Choice from ТУ_р.Состояние = Загрузка
      first
      Convert_begin
        Состояние set Загружается
      Convert_end
        Состояние set Загружен
  Пер_партия
     Choice from  Пер_партия.Сост = Перевозка and  Пер_партия.Транспорт = ТУ_р.Номер
       first
       Convert_begin
         Сост set Транспортируется
  Накоп
     Choice from Накоп.Идентификатор = Пер_партия.Место
       first
       Convert_end
         Свободно set Накоп.Свободно + Пер_партия.Размер
$End

$Pattern Начало_транспортировки : rule
$Relevant_resources
  Роб_1 : ТУ  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Загружен
      first
      Convert_rule
        Состояние   set Проверка_узла1
        Сл_величина set 0.0
        Номер_шага  set 0
        Номер_узла  set Нак_Узлы(Роб_1.Позиция)
        Приоритет   set 0.0
$End

$Pattern Транспортировка : operation
$Relevant_resources
  Роб_1      : ТУ          Keep     Keep
  Пер_партия : Пер_партии  NoChange NoChange
$Time= (Расстояние(Роб_1.Номер_узла, Роб_1.След_узел) / Роб_1.Скорость) * Масштаб_времени
$Body
  Роб_1
    Choice from Роб_1.Состояние = Проверено1 and Роб_1.Номер_узла <> Нак_Узлы(Роб_1.Куда) and
                Расстояние(Роб_1.Номер_узла, Роб_1.След_узел) > 0
      first
      Convert_begin
        Состояние   set Движение_заг
        Пред_узел   set Роб_1.Номер_узла
        Х_коор      set Координаты_узлов(Х,Роб_1.Номер_узла)
        У_коор      set Координаты_узлов(У,Роб_1.Номер_узла)
      Convert_end
        Состояние   set Проверка_узла1
        Сл_величина set 0.0
        Номер_шага  set 0
        Номер_узла  set Роб_1.След_узел
        Х_коор      set Координаты_узлов(Х,Роб_1.Номер_узла)
        У_коор      set Координаты_узлов(У,Роб_1.Номер_узла)
        Приоритет   set 0.0
  Пер_партия
    Choice from Пер_партия.Сост = Транспортируется and Пер_партия.Транспорт = Роб_1.Номер
      first
$End

$Pattern Конец_транспортировки : rule
$Relevant_resources
  Роб_1      : ТУ          Keep
  Пер_партия : Пер_партии  NoChange
  Накоп      : Накопители  Keep
$Body
  Роб_1
    Choice from Роб_1.Состояние = Проверка_узла1 and Роб_1.Номер_узла = Нак_Узлы(Роб_1.Куда)
      first
      Convert_rule
        Позиция   set Роб_1.Куда
        Состояние set Разгрузка
  Пер_партия
    Choice from Пер_партия.Сост = Транспортируется and Пер_партия.Транспорт = Роб_1.Номер
      first
  Накоп
    Choice from Накоп.Идентификатор = Пер_партия.Сл_место
      first
      Convert_rule
        Ном_детали set Пер_партия.Номер
$End

$Pattern Разгрузка_ТУ : operation
$Relevant_resources
  ТУ_р       : ТУ          Keep     Keep
  Пер_партия : Пер_партии  NoChange Keep
  Накоп      : Накопители  NoChange Keep
$Time = ТУ_р.Скорость_пог * Пер_партия.Размер * Масштаб_времени
$Body
  ТУ_р
    Choice from ТУ_р.Состояние = Разгрузка
     first
     Convert_begin
       Состояние set Разгружается
     Convert_end
       Состояние set Свободен
  Пер_партия
    Choice from  Пер_партия.Сост = Транспортируется and Пер_партия.Транспорт = ТУ_р.Номер
      first
      Convert_end
        Сост      set Вызов_со_склада(Пер_партия.Сл_место)
        Транспорт set 0
        Место     set Пер_партия.Сл_место
        Сл_место  set Тех_путь(Пер_партия.Номер_операции,Пер_партия.Тип)
  Накоп
    Choice from Накоп.Идентификатор = Пер_партия.Сл_место
      first
      Convert_end
        Ном_детали set Пер_партия.Номер
$End

$Pattern Начало_обработки : rule
$Relevant_resources
  Пер_партия : Пер_партии  Keep
  ОТО_р      : ОТО         Keep
$Body
  Пер_партия
    Choice from Пер_партия.Сост = Ожидание and Пер_партия.Место <> Складвх
      with_min Пер_партия.Номер
      Convert_rule
        Сост set Обработка
  ОТО_р
    Choice from ОТО_р.Состояние = Свободно and ОТО_р.Нак_ОТО = Пер_партия.Место
      first
      Convert_rule
        Номер_дет   set Пер_партия.Номер
        Состояние   set Занято
        Обраб_дет   set 0
        Разм_партии set Пер_партия.Размер
$End

$Pattern Операция_обработки : operation
$Relevant_resources
  ОТО_р      : ОТО         Keep     Keep
  Пер_партия : Пер_партии  NoChange NoChange
$Time = Времена(Пер_партия.Номер_операции,Пер_партия.Тип) * Масштаб_времени
$Body
  ОТО_р
    Choice from ОТО_р.Состояние = Занято and
                ОТО_р.Обраб_дет < ОТО_р.Разм_партии and
                ОТО_р.Авария = Нет
      first
      Convert_begin
        Состояние set Работа
     Convert_end
        Состояние set Занято
        Обраб_дет set ОТО_р.Обраб_дет + 1
  Пер_партия
    Choice from Пер_партия.Сост = Обработка and Пер_партия.Место = ОТО_р.Нак_ОТО
      first
$End

$Pattern Конец_обработки : rule
$Relevant_resources
  ОТО_р      : ОТО         Keep
  Пер_партия : Пер_партии  Keep
$Body
  ОТО_р
    Choice from ОТО_р.Состояние = Занято and
                ОТО_р.Обраб_дет >= ОТО_р.Разм_партии
      first
      Convert_rule
        Состояние set Свободно
        Обраб_дет set 0
  Пер_партия
    Choice from Пер_партия.Сост = Обработка and
                Пер_партия.Место = ОТО_р.Нак_ОТО
      first
      Convert_rule
        Номер_операции set Пер_партия.Номер_операции + 1
        Сост           set Перевезти
        Сл_место       set Тех_путь(Пер_партия.Номер_операции,Пер_партия.Тип)
$End

$Pattern Ремонтировать : rule
$Relevant_resources
  ОТО_р      : ОТО         Keep
  Пер_партия : Пер_партии  NoChange
$Body
  ОТО_р
    Choice from ОТО_р.Состояние = Занято and
                ОТО_р.Авария = Да
      first
      Convert_rule
        Состояние set Останов
  Пер_партия
    Choice from Пер_партия.Сост = Обработка and
                Пер_партия.Номер = ОТО_р.Номер_дет and
                Пер_партия.Место = ОТО_р.Нак_ОТО
      first
$End

$Pattern Ремонт_ОТО : operation
$Relevant_resources
  ОТО_р : ОТО  Keep Keep
$Time = Равномерный_Закон(ОТО_р.Время_мин, ОТО_р.Время_макс) * Масштаб_времени
$Body
  ОТО_р
    Choice from ОТО_р.Состояние = Останов and
                ОТО_р.Авария = Да
      first
      Convert_begin
        Состояние set Ремонт
     Convert_end
        Состояние set Занято
        Авария    set Нет
        Обраб_дет set ОТО_р.Обраб_дет - 1
$End

$Pattern Вызов_ТУ : rule
$Relevant_resources
  Пер_партия : Пер_партии  Keep
  ТУ_р       : ТУ          Keep
  Накоп      : Накопители  Keep
$Body
  Пер_партия
    Choice from Пер_партия.Сост = Перевезти
      with_min  Пер_партия.Номер
      Convert_rule
        Сост      set Перевозка
        Транспорт set ТУ_р.Номер
  ТУ_р
    Choice from ТУ_р.Состояние = Свободен  and
		Нак_ТУ(Пер_партия.Место, ТУ_р.Номер) = 1 and
		Нак_ТУ(Пер_партия.Сл_место, ТУ_р.Номер) = 1
      with_min Нак_Узлы(Пер_партия.Место) - Нак_Узлы(ТУ_р.Позиция)
      Convert_rule
        Состояние set Вызов
        Куда      set Пер_партия.Место
  Накоп
    Choice from Накоп.Идентификатор = Пер_партия.Сл_место and
                Накоп.Свободно >= Пер_партия.Размер
      first
      Convert_rule
        Свободно  set Накоп.Свободно - Пер_партия.Размер
$End

$Pattern Вызов_ТУ_на_склад : rule
$Relevant_resources
  Пер_партия : Пер_партии  Keep
  ТУ_р       : ТУ          Keep
  Накоп      : Накопители  NoChange
$Body
  Пер_партия
    Choice from Пер_партия.Сост = Перевезти and Пер_партия.Место <> Складвх
      with_min Пер_партия.Номер
      Convert_rule
        Сост      set Перевозка
        Транспорт set ТУ_р.Номер
        Сл_место  set Складвх
  ТУ_р
    Choice from ТУ_р.Состояние = Свободен   and
		Нак_ТУ(Пер_партия.Место, ТУ_р.Номер) = 1 and
		Нак_ТУ(Пер_партия.Сл_место, ТУ_р.Номер) = 1
      with_min Нак_Узлы(Пер_партия.Место) - Нак_Узлы(ТУ_р.Позиция)
      Convert_rule
        Состояние set Вызов
        Куда      set Пер_партия.Место
  Накоп
    Choice from Накоп.Идентификатор = Пер_партия.Сл_место and
                Накоп.Свободно < Пер_партия.Размер
      first
$End

$Pattern Освободить_вых : rule
$Relevant_resources
  Склвых     : Накопитель_вых  Keep
  Система_2  : Система         Keep
  Пер_партия : Пер_партии      Erase
$Body
  Склвых
    Choice NoCheck
      first
      Convert_rule
        Свободно set Склвых.Свободно + Пер_партия.Размер
  Система_2
    Choice NoCheck
      first
      Convert_rule
        Обраб_деталей set Система_2.Обраб_деталей + Пер_партия.Размер
  Пер_партия
    Choice from Пер_партия.Место = Складвых
      first
$End

$Pattern Движение_тр : operation
$Parameters
  Номер_ТУ : integer
$Relevant_resources
  Роб_1 : ТУ  Keep Keep
$Time= (Дискрета_движения / Роб_1.Скорость) * Масштаб_времени
$Body
  Роб_1
    Choice from Роб_1.Номер = Номер_ТУ and Роб_1.Анимация = Стоп and [Роб_1.Состояние = Движение_св or Роб_1.Состояние = Движение_заг]
     first
     Convert_begin
       Х_коор   set Роб_1.Х_коор + Дискрета_движения * (Координаты_узлов(Х,Роб_1.След_узел) - Координаты_узлов(Х,Роб_1.Номер_узла)) / Расстояние(Роб_1.Номер_узла, Роб_1.След_узел)
       У_коор   set Роб_1.У_коор + Дискрета_движения * (Координаты_узлов(У,Роб_1.След_узел) - Координаты_узлов(У,Роб_1.Номер_узла)) / Расстояние(Роб_1.Номер_узла, Роб_1.След_узел)
       Анимация set Кадр
     Convert_end
       Анимация set Стоп
$End

$Pattern _Масштаб : keyboard
$Parameters
  Куда : integer
$Relevant_resources
  Константы : Const Keep Keep
$Time = 0.0
$Body
  Константы
  Choice from [Константы.Counter = 0] and
              [[Куда = +1 and [[Константы.dX < 800 - (Константы.minX + 2)* Константы.Scale] and
                               [Константы.dY < 484 - (Константы.minY + 2)* Константы.Scale]]] or
               [Куда = -1 and [[Константы.dX > (2 - Константы.maxX) * Константы.Scale] and
                               [Константы.dY > (2 - Константы.maxY) * Константы.Scale]]]]
  first
    Convert_begin
      Scale   set Константы.Scale + Куда * Константы.Scale / 20
      Counter set Константы.Counter + 1
    Convert_end
      Counter set Константы.Counter - 1
$End

$Pattern _Прокрутка : keyboard
$Parameters
  Горизонталь: integer
  Вертикаль  : integer
  Куда       : integer
$Relevant_resources
  Константы : Const Keep Keep
$Time = 0.0
$Body
  Константы
  Choice from [Константы.Counter = 0] and
              [[Горизонталь = 1 and [[Куда = +1 and Константы.dX < 800 - (Константы.minX + 2)* Константы.Scale] or
                                     [Куда = -1 and Константы.dX > (2 - Константы.maxX) * Константы.Scale]]] or
               [Вертикаль   = 1 and [[Куда = +1 and Константы.dY < 484 - (Константы.minY + 2)* Константы.Scale] or
                                     [Куда = -1 and Константы.dY > (2 - Константы.maxY) * Константы.Scale]]]]
  first
    Convert_begin
      dX      set Константы.dX + Горизонталь * Куда * Константы.Scale
      dY      set Константы.dY + Вертикаль   * Куда * Константы.Scale
      Counter set Константы.Counter + 1
    Convert_end
      Counter set Константы.Counter - 1
$End

$Pattern _Сброс_прокрутки : keyboard
$Relevant_resources
  Константы : Const Keep Keep
$Time = 0.0
$Body
  Константы
  Choice from Константы.Counter = 0
  first
    Convert_begin
      dX      set 0
      dY      set 0
      Counter set Константы.Counter + 1
    Convert_end
      Counter set Константы.Counter - 1
$End