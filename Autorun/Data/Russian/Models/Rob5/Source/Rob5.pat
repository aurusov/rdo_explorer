$Pattern  Назначение_1_2  : rule
$Parameters
  Номер_конвейера  : integer
  Куда_ехать       : such_as Роботы.Место
  Загруженность_ПС : such_as Станки.Загруженность
  Вид_детали       : such_as Детали.Вид
  Место_детали     : such_as Детали.Место

$Relevant_resources
  Откуда   : Конвейеры           NoChange
  Позиция  : Позиция_конвейера   NoChange
  Деталь   : Детали              NoChange
  Тр_Робот : ТР                  Keep
  Куда     : ПС                  NoChange

$Body
  Откуда
    Choice from  Откуда.Номер = Номер_конвейера and
                 Откуда.Количество_деталей > 0
    first

  Позиция
    Choice from Позиция.Номер_конвейера = Номер_конвейера   and
                Позиция.Номер_позиции   = Откуда.Положение  and
                Позиция.Состояние       = Полна
    first

  Деталь
    Choice from  Деталь.Вид = Вид_детали and
                 Деталь.Место = Место_детали
    first

  Тр_Робот
    Choice from Тр_Робот.Занятость     = Свободен
    first
    Convert_rule
      Занятость              set Движется_пуст
      Место_движения         set Куда_ехать

  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Загруженность_ПС
    first
$End

$Pattern  Погрузка_с_К1_2  : operation
$Parameters
  Номер_конвейера  : integer
  Куда_ехать       : such_as Роботы.Место
  Загруженность_ПС : such_as Станки.Загруженность
  Вид_детали       : such_as Детали.Вид
  Место_детали     : such_as Детали.Место

$Relevant_resources
  Откуда   : Конвейеры           NoChange  Keep
  Позиция  : Позиция_конвейера   NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep
  Куда     : ПС                  NoChange  NoChange

$Time = Время_погрузки
$Body
  Откуда
    Choice from  Откуда.Номер = Номер_конвейера and
                 Откуда.Количество_деталей > 0
    first
    Convert_end
      Количество_деталей     set Откуда.Количество_деталей - 1

  Позиция
    Choice from Позиция.Номер_конвейера = Номер_конвейера and
                Позиция.Номер_позиции = Откуда.Положение  and
                Позиция.Состояние = Полна
    first
    Convert_end
      Состояние        set  Пуста

  Деталь
    Choice from  Деталь.Вид = Вид_детали and
                 Деталь.Место = Место_детали
    first
    Convert_end
      Место          set Робот

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_пуст  and
                Тр_Робот.Место_движения = Куда_ехать     and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Погрузка
      Место          set Куда_ехать
    Convert_end
      Занятость      set Движется_полон
      Место_движения set ПозСборки

  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Загруженность_ПС
    first
$End

$Pattern  Выгрузка_на_Сборку  : operation
$Parameters
  Загруженность_ПС       : such_as Станки.Загруженность
  Новая_Загруженность_ПС : such_as Станки.Загруженность

$Relevant_resources
  Куда     : ПС           NoChange  Keep
  Деталь   : Детали       NoChange  Keep
  Тр_Робот : ТР           Keep      Keep

$Time = Время_выгрузки
$Body
  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Загруженность_ПС
    first
    Convert_end
      Загруженность          set Новая_Загруженность_ПС

  Деталь
    Choice from  Деталь.Место = Робот
    first
    Convert_end
      Место          set ПозСборки

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_полон and
                Тр_Робот.Место_движения = ПозСборки      and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Разгрузка
      Место          set ПозСборки
    Convert_end
      Занятость      set Свободен
      Место_движения set Нет
$End

$Pattern  Назначение_3  : rule

$Relevant_resources
  Откуда   : ПС                  NoChange
  Деталь   : Детали              NoChange
  Тр_Робот : ТР                  Keep
  Куда     : ПО                  NoChange
  Позиция  : Позиция_конвейера   NoChange

$Body
  Откуда
    Choice from Откуда.Занятость     = Свободен  and
                Откуда.Загруженность = Обработано
    first

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ПозСборки
    first

  Тр_Робот
    Choice from Тр_Робот.Занятость     = Свободен
    first
    Convert_rule
      Занятость              set Движется_пуст
      Место_движения         set ПозСборки

  Куда
    Choice from Куда.Количество_деталей < 10
    first

  Позиция
    Choice from Позиция.Номер_конвейера = 4    and
                Позиция.Номер_позиции   = Последняя_позиция(Куда.Положение, 10) and
                Позиция.Состояние       = Пуста
    first
$End

$Pattern  Погрузка_с_ПС  : operation

$Relevant_resources
  Откуда   : ПС                  NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep
  Куда     : ПО                  NoChange  NoChange
  Позиция  : Позиция_конвейера   NoChange  NoChange

$Time = Время_погрузки
$Body
  Откуда
    Choice from Откуда.Занятость     = Свободен  and
                Откуда.Загруженность = Обработано
    first
    Convert_end
      Загруженность  set Разгружен

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ПозСборки
    first
    Convert_end
      Место          set Робот

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_пуст  and
                Тр_Робот.Место_движения = ПозСборки      and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Погрузка
      Место          set ПозСборки
    Convert_end
      Занятость      set Движется_полон
      Место_движения set ПозОхл_вход

  Куда
    Choice from Куда.Количество_деталей < 10
    first

  Позиция
	 Choice from Позиция.Номер_конвейера = 4    and
					 Позиция.Номер_позиции   = Последняя_позиция(Куда.Положение, 10) and
					 Позиция.Состояние       = Пуста
	 first
$End

$Pattern  Выгрузка_на_ПО  : operation

$Relevant_resources
  Куда     : ПО                  Keep      Keep
  Позиция  : Позиция_конвейера   NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep

$Time = Время_выгрузки
$Body
  Куда
	 Choice from Куда.Количество_деталей < 10 and
					 Куда.Движение = Стоит
	 first
	 Convert_begin
		Движение            set Блокирован
	 Convert_end
		Количество_деталей  set Куда.Количество_деталей + 1
		Движение            set Стоит

  Позиция
	 Choice from Позиция.Номер_конвейера = 4  and
					 Позиция.Номер_позиции = Последняя_позиция(Куда.Положение, 10) and
					 Позиция.Состояние = Пуста
	 first
	 Convert_end
		Состояние        set  Полна

  Деталь
	 Choice from  Деталь.Место = Робот
	 first
	 Convert_end
		Место          set ПозОхл
		Время_на_ПО    set Time_now

  Тр_Робот
	 Choice from Тр_Робот.Занятость      = Движется_полон and
					 Тр_Робот.Место_движения = ПозОхл_вход    and
					 Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
	 first
	 Convert_begin
      Занятость      set Разгрузка
      Место          set ПозОхл_вход
    Convert_end
      Занятость      set Свободен
      Место_движения set Нет
$End

$Pattern  Назначение_4  : rule

$Relevant_resources
  Откуда   : ПО                  NoChange
  Позиция  : Позиция_конвейера   NoChange
  Деталь   : Детали              NoChange
  Тр_Робот : ТР                  Keep
  Куда     : ШС                  NoChange

$Body
  Откуда
    Choice from  Откуда.Количество_деталей > 0
    first

  Позиция
    Choice from Позиция.Номер_конвейера = 4  and
                Позиция.Номер_позиции   = Откуда.Положение  and
                Позиция.Состояние       = Полна
    first

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ПозОхл
    first

  Тр_Робот
    Choice from Тр_Робот.Занятость     = Свободен
    first
	 Convert_rule
      Занятость              set Движется_пуст
      Место_движения         set ПозОхл_выход

  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Разгружен
    first
$End

$Pattern  Погрузка_с_ПО  : operation

$Relevant_resources
  Откуда   : ПО                  NoChange  Keep
  Позиция  : Позиция_конвейера   NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep
  Куда     : ШС                  NoChange  NoChange

$Time = Время_погрузки
$Body
  Откуда
    Choice from  Откуда.Количество_деталей > 0
    first
    Convert_end
      Количество_деталей     set Откуда.Количество_деталей - 1

  Позиция
    Choice from Позиция.Номер_конвейера = 4 and
                Позиция.Номер_позиции = Откуда.Положение  and
                Позиция.Состояние = Полна
    first
    Convert_end
      Состояние        set  Пуста

  Деталь
	 Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ПозОхл
    first
	 Convert_end
		Место          set Робот
		Время_на_ПО    set Time_now - Деталь.Время_на_ПО

  Тр_Робот
	 Choice from Тр_Робот.Занятость      = Движется_пуст  and
					 Тр_Робот.Место_движения = ПозОхл_выход   and
					 Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
	 first
	 Convert_begin
		Занятость      set Погрузка
		Место          set ПозОхл_выход
	 Convert_end
		Занятость      set Движется_полон
		Место_движения set ШлифСтанок

  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Разгружен
    first
$End

$Pattern  Выгрузка_на_ШС  : operation

$Relevant_resources
  Куда     : ШС           NoChange  Keep
  Деталь   : Детали       NoChange  Keep
  Тр_Робот : ТР           Keep      Keep

$Time = Время_выгрузки
$Body
  Куда
    Choice from Куда.Занятость     = Свободен  and
                Куда.Загруженность = Разгружен
    first
    Convert_end
      Загруженность          set Загружен

  Деталь
    Choice from  Деталь.Место = Робот
    first
    Convert_end
      Место          set ШлифСтанок

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_полон and
                Тр_Робот.Место_движения = ШлифСтанок     and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Разгрузка
      Место          set ШлифСтанок
    Convert_end
      Занятость      set Свободен
      Место_движения set Нет
$End

$Pattern  Назначение_5  : rule

$Relevant_resources
  Откуда   : ШС                  NoChange
  Деталь   : Детали              NoChange
  Тр_Робот : ТР                  Keep
  Куда     : К3                  NoChange
  Позиция  : Позиция_конвейера   NoChange

$Body
  Откуда
    Choice from Откуда.Занятость    <> Занят  and
                Откуда.Загруженность = Обработано
    first

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ШлифСтанок
    first

  Тр_Робот
    Choice from Тр_Робот.Занятость     = Свободен
    first
    Convert_rule
      Занятость              set Движется_пуст
      Место_движения         set ШлифСтанок

  Куда
    Choice from Куда.Количество_деталей < 6
    first

  Позиция
    Choice from Позиция.Номер_конвейера = 3    and
                Позиция.Номер_позиции   = Последняя_позиция(Куда.Положение, 6) and
                Позиция.Состояние       = Пуста
    first
$End

$Pattern  Погрузка_с_ШС  : operation

$Relevant_resources
  Откуда   : ШС                  NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep
  Куда     : К3                  NoChange  NoChange
  Позиция  : Позиция_конвейера   NoChange  NoChange

$Time = Время_погрузки
$Body
  Откуда
    Choice from Откуда.Занятость     <> Занят  and
                Откуда.Загруженность = Обработано
    first
    Convert_end
      Загруженность  set Разгружен

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ШлифСтанок
    first
    Convert_end
      Место          set Робот

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_пуст  and
                Тр_Робот.Место_движения = ШлифСтанок     and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Погрузка
      Место          set ШлифСтанок
    Convert_end
      Занятость      set Движется_полон
      Место_движения set Конвейер_3

  Куда
    Choice from Куда.Количество_деталей < 6
    first

  Позиция
    Choice from Позиция.Номер_конвейера = 3   and
                Позиция.Номер_позиции   = Последняя_позиция(Куда.Положение, 6) and
                Позиция.Состояние       = Пуста
    first
$End

$Pattern  Выгрузка_на_К3  : operation

$Relevant_resources
  Куда     : К3                  Keep      Keep
  Позиция  : Позиция_конвейера   NoChange  Keep
  Деталь   : Детали              NoChange  Keep
  Тр_Робот : ТР                  Keep      Keep

$Time = Время_выгрузки
$Body
  Куда
    Choice from Куда.Количество_деталей < 6 and
                Куда.Движение = Стоит
    first
    Convert_begin
      Движение            set Блокирован
    Convert_end
      Количество_деталей  set Куда.Количество_деталей + 1
      Движение            set Стоит

  Позиция
    Choice from Позиция.Номер_конвейера = 3  and
                Позиция.Номер_позиции = Последняя_позиция(Куда.Положение, 6) and
                Позиция.Состояние = Пуста
    first
    Convert_end
      Состояние        set  Полна

  Деталь
    Choice from  Деталь.Место = Робот
    first
    Convert_end
      Место          set Конвейер_3

  Тр_Робот
    Choice from Тр_Робот.Занятость      = Движется_полон and
                Тр_Робот.Место_движения = Конвейер_3     and
                Тр_Робот.Координата     = ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Занятость      set Разгрузка
      Место          set Конвейер_3
    Convert_end
      Занятость      set Свободен
      Место_движения set Нет
$End

$Pattern  Движение_влево  : operation

$Relevant_resources
  Тр_Робот : Роботы      Keep   Keep

$Time = 0.02 / Скорость_робота
$Body
  Тр_Робот
	 Choice from [Тр_Робот.Занятость = Движется_пуст or
					 Тр_Робот.Занятость = Движется_полон] and
					 Тр_Робот.Перемещение = Нет_перемещения and
					 Тр_Робот.Место_движения <> Нет and
					 Тр_Робот.Координата > ТР_Координата(Тр_Робот.Место_движения)
	 first
	 Convert_begin
		Перемещение            set Движется_влево
	 Convert_end
		Координата             set Тр_Робот.Координата - 1
		Перемещение            set Нет_перемещения
$End

$Pattern  Движение_вправо  : operation

$Relevant_resources
  Тр_Робот : Роботы      Keep   Keep

$Time = 0.02 / Скорость_робота
$Body
  Тр_Робот
    Choice from [Тр_Робот.Занятость = Движется_пуст or
                Тр_Робот.Занятость = Движется_полон] and
                Тр_Робот.Перемещение = Нет_перемещения and
                Тр_Робот.Место_движения <> Нет and
                Тр_Робот.Координата < ТР_Координата(Тр_Робот.Место_движения)
    first
    Convert_begin
      Перемещение            set Движется_вправо
    Convert_end
      Координата             set Тр_Робот.Координата + 1
      Перемещение            set Нет_перемещения
$End

$Pattern  Сборка        : operation
$Parameters
  Время_операции  : real

$Relevant_resources
  Станок     : ПС      Keep       Keep
  Деталь_1   : Детали  NoChange   Keep
  Деталь_2   : Детали  NoChange   Erase

$Time = Время_операции
$Body
  Станок
    Choice from Станок.Занятость     = Свободен  and
                Станок.Загруженность = Загружен
    first
    Convert_begin
      Занятость      set Занят
    Convert_end
      Занятость      set Свободен
      Загруженность  set Обработано

  Деталь_1
    Choice from  Деталь_1.Вид = вал and
                 Деталь_1.Место = ПозСборки
    first
    Convert_end
      Вид            set ротор

  Деталь_2
    Choice from  Деталь_2.Вид = крыльчатка and
                 Деталь_2.Место = ПозСборки
    first

$End

$Pattern  Шлифование        : operation
$Parameters
  Время_операции  : real

$Relevant_resources
  Станок     : ШС         Keep       Keep
  Деталь     : Детали     NoChange   NoChange

$Time = Время_операции
$Body
  Станок
    Choice from Станок.Занятость     = Свободен  and
                Станок.Загруженность = Загружен
    first
    Convert_begin
      Занятость      set Занят
      Хранитель      set Станок.Количество_отказов
    Convert_end
      Занятость      set Новая_Занятость(Станок.Количество_отказов - Станок.Хранитель,
                                         Свободен, Станок.Занятость)
      Загруженность  set Новая_Загруженность(Станок.Количество_отказов - Станок.Хранитель,
                                             Обработано, Станок.Загруженность)
  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = ШлифСтанок
    first
$End

$Pattern  Поступление_комплектующих : irregular_event
$Parameters
  Интервал  : real

$Relevant_resources
  Конвейер_1 : К1                 Keep
  Конвейер_2 : К2                 Keep
  Деталь_вал        : Детали      Create
  Деталь_крыльчатка : Детали      Create

$Time = Последовательность_1(Интервал, 0.1)
$Body
  Конвейер_1
	 Convert_event
		Количество_деталей  set Конвейер_1.Количество_деталей + 1

  Конвейер_2
    Convert_event
      Количество_деталей  set Конвейер_2.Количество_деталей + 1

  Деталь_вал
    Convert_event
      Время_поступления  set Time_now
      Вид                set вал
      Место              set у_Конвейер_1
      Время_на_ПО        set 0.0

  Деталь_крыльчатка
    Convert_event
      Время_поступления  set Time_now
      Вид                set крыльчатка
      Место              set у_Конвейер_2
      Время_на_ПО        set 0.0
$End

$Pattern  Размещение_комплектующих        : rule
$Parameters
  Номер_конвейера  : integer
  Старое_место     : such_as Детали.Место
  Новое_место      : such_as Детали.Место

$Relevant_resources
  Деталь       : Детали             Keep
  Конвейер     : Конвейеры          NoChange
  Позиция      : Позиция_конвейера  Keep

$Body
  Деталь
    Choice from  Деталь.Место = Старое_место
    first
    Convert_rule
      Место      set Новое_место

  Конвейер
    Choice from  Конвейер.Номер = Номер_конвейера and
                 Конвейер.Количество_деталей > 0
    first

  Позиция
    Choice from Позиция.Номер_конвейера = Номер_конвейера and
                Позиция.Номер_позиции   = Последняя_позиция(Конвейер.Положение, 6) and
                Позиция.Состояние       = Пуста
    first
    Convert_rule
      Состояние  set Полна
$End

$Pattern  Отвозка_роторов        : operation
$Parameters
  Время_операции      : real
  Количество_готовых  : integer

$Relevant_resources
  Конвейер        : К3                  Keep      NoChange
  Послед_позиция  : Позиция_конвейера   Keep      NoChange
  Деталь          : Детали              NoChange  Erase

$Time = Время_операции

$Body
  Конвейер
    Choice from  Конвейер.Количество_деталей >= Количество_готовых
    first
    Convert_begin
      Количество_деталей  set Конвейер.Количество_деталей - 1

  Послед_позиция
    Choice from Послед_позиция.Номер_конвейера = 3 and
                Послед_позиция.Номер_позиции   = Последняя_позиция(Конвейер.Положение, 6) and
                Послед_позиция.Состояние       = Полна
    first
    Convert_begin
      Состояние           set Пуста

  Деталь
    Choice from  Деталь.Вид = ротор and
                 Деталь.Место = Конвейер_3
    first
$End

$Pattern  Движение_Конвейера_1_2  : operation
$Parameters
  Время_операции  : real
  Номер_конвейера : integer

$Relevant_resources
  Конвейер  : Конвейеры           Keep       Keep
  Позиция   : Позиция_конвейера   NoChange   NoChange

$Time = Время_операции

$Body
  Конвейер
    Choice from  Конвейер.Номер = Номер_конвейера  and
                 Конвейер.Количество_деталей > 0   and
                 Конвейер.Движение = Стоит
    first
    Convert_begin
      Движение            set  Едет
    Convert_end
      Положение           set  Новое_положение(Конвейер.Положение, 6)
      Движение            set  Стоит

  Позиция
    Choice from Позиция.Номер_конвейера = Номер_конвейера     and
                Позиция.Номер_позиции   = Конвейер.Положение  and
                Позиция.Состояние       = Пуста
    first
$End

$Pattern  Движение_Конвейера_3  : operation

$Parameters
  Время_операции  : real

$Relevant_resources
  Конвейер        : К3                  Keep       Keep
  Первая_позиция  : Позиция_конвейера   NoChange   NoChange
  Послед_позиция  : Позиция_конвейера   NoChange   NoChange

$Time = Время_операции

$Body
  Конвейер
    Choice from  Конвейер.Количество_деталей > 0 and
                 Конвейер.Движение        = Стоит
    first
    Convert_begin
      Движение            set  Едет
    Convert_end
      Положение           set  Новое_положение(Конвейер.Положение, 6)
      Движение            set  Стоит

  Первая_позиция
    Choice from Первая_позиция.Номер_конвейера = 3 and
                Первая_позиция.Номер_позиции   = Конвейер.Положение and
                Первая_позиция.Состояние       = Пуста
    first

  Послед_позиция
    Choice from Послед_позиция.Номер_конвейера = 3 and
                Послед_позиция.Номер_позиции   = Последняя_позиция(Конвейер.Положение, 6) and
                Послед_позиция.Состояние       = Полна
    first
$End

$Pattern  Движение_ПО  : operation
$Parameters
  Время_операции  : real

$Relevant_resources
  Конвейер  : ПО                  Keep       Keep
  Позиция   : Позиция_конвейера   NoChange   NoChange

$Time = Время_операции

$Body
  Конвейер
    Choice from  Конвейер.Количество_деталей > 0   and
                 Конвейер.Движение = Стоит
    first
    Convert_begin
      Движение            set  Едет
    Convert_end
      Положение           set  Новое_положение(Конвейер.Положение, 10)
      Движение            set  Стоит

  Позиция
    Choice from Позиция.Номер_конвейера = 4     and
                Позиция.Номер_позиции   = Конвейер.Положение  and
                Позиция.Состояние       = Пуста
    first
$End

$Pattern  Отказ_ШС : irregular_event
$Parameters
  Интервал  : real

$Relevant_resources
  Станок     : ШС         Keep

$Time = Последовательность_2(Интервал)
$Body
  Станок
    Convert_event
      Занятость           set Сломан
      Количество_отказов  set Станок.Количество_отказов + 1
$End

$Pattern  Восстановление_ШС        : operation
$Parameters
  Среднее_время  : real

$Relevant_resources
  Станок     : ШС         Keep       Keep

$Time = Последовательность_3(Среднее_время, Среднее_время / 5)
$Body
  Станок
    Choice from Станок.Занятость = Сломан
    first
    Convert_begin
      Занятость      set Восстановление
    Convert_end
		Занятость      set Свободен
$End
